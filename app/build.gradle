apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()

keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        signed {
            keyAlias keystoreProperties["keyAlias"]
            keyPassword keystoreProperties["keyPassword"]
            storeFile file(keystoreProperties["storeFile"])
            storePassword keystoreProperties["storePassword"]
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    compileSdkVersion 30
    defaultConfig {
        applicationId "pl.baftek.buswakeup"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 6
        versionName "1.1.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.signed
        }
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }
}

dependencies {
    def room_version = "2.2.6"
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    // Google
    implementation "com.google.android.material:material:1.3.0"
    // Play Services
    implementation "com.google.android.gms:play-services-maps:17.0.0"
    implementation "com.google.android.gms:play-services-location:18.0.0"
    // AndroidX
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "androidx.core:core-ktx:1.5.0-beta03"
    implementation "androidx.fragment:fragment-ktx:1.3.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    // AppIntro
    implementation "com.github.AppIntro:AppIntro:5.1.0"
    // Test
    testImplementation "junit:junit:4.13"
    androidTestImplementation "androidx.test:runner:1.4.0-alpha05"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0-alpha05"
}
